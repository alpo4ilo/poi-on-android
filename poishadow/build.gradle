plugins {
    id 'java' // java plugin must be explicitly applied for shadow plugin
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.poi:poi-ooxml:5.2.3'
    implementation 'org.apache.poi:poi-ooxml-full:5.2.3'
    implementation 'org.apache.poi:poi-scratchpad:5.2.3'
    implementation ('fr.opensagres.xdocreport:fr.opensagres.poi.xwpf.converter.xhtml:2.0.4')
    implementation 'com.fasterxml:aalto-xml:1.3.0'
    implementation 'stax:stax-api:1.0.1'

    // additional for PDF converter instead .converter.xhtml
    //implementation("fr.opensagres.xdocreport:fr.opensagres.xdocreport.converter.docx.xwpf:2.0.4")
}

shadowJar {
    exclude 'repackage/**'

    exclude 'LICENSE.txt'
    exclude 'NOTICE.txt'
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/LICENSE.txt'
    exclude 'META-INF/NOTICE'
    exclude 'META-INF/NOTICE.txt'
    exclude 'META-INF/maven/**'

    // these are only needed when handling Visio files, remove it if you would like to use Visio support
    dependencies {
        exclude(dependency('com.github.virtuald:curvesapi'))
        exclude(dependency('commons-codec:commons-codec'))
        exclude(dependency('commons-codec:commons-collections4'))
        exclude(dependency('org.apache.commons:commons-collections4'))
        exclude(dependency('commons-io:commons-io'))

        exclude(dependency('org.apache.commons:commons-math3'))
        exclude(dependency('org.apache.commons:commons-text'))
        exclude(dependency('org.apache.commons:commons-lang3'))
    }

    // Relocate javax dependencies so Android does not choke
    relocate 'javax.xml.namespace', 'org.apache.poi.javax.xml.namespace'
    relocate 'javax.xml.stream', 'org.apache.poi.javax.xml.stream'
    relocate 'javax.xml.XMLConstants', 'org.apache.poi.javax.xml.XMLConstants'
    // java.awt is not available, but class Color is used in APIs in POI, therefore
    // relocate this class to another one where we can include a rewrite
    relocate 'java.awt.Color', 'org.apache.poi.java.awt.Color'
}

jar.dependsOn shadowJar
test.dependsOn shadowJar
